provider "aws" {
  region = "us-east-1"
}

data "aws_availability_zones" "all" {}

data "terraform_remote_state" "db" {
  backend = "local"
  config {
    path = "/Users/neo/workspace/terraform/state-store/data-storage/mysql/terraform.tfstate"
  }
}

resource "aws_launch_configuration" "lc" {
  image_id        = "ami-40d28157"
  instance_type   = "t2.micro"
  security_groups = ["${aws_security_group.sg.id}"]

  user_data = <<-EOF
              #!/bin/bash
              echo "<html>" > index.html
              echo "<h1>Hello, World $(hostname)</h1>" >> index.html
              echo "<br>" >> index.html
              echo "<h2>${data.terraform_remote_state.db.address}<h2>" >> index.html
              echo "<h2>${data.terraform_remote_state.db.port}<h2>" >> index.html
              nohup busybox httpd -f -p "${var.server_port}" &
              EOF

  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_security_group" "sg" {
  name = "benedetto-sg-asg"

  ingress {
    from_port   = "${var.server_port}"
    to_port     = "${var.server_port}"
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_autoscaling_group" "asg" {
  launch_configuration = "${aws_launch_configuration.lc.id}"
  availability_zones = ["${data.aws_availability_zones.all.names}"]

  load_balancers = ["${aws_elb.elb.name}"]
  health_check_type = "ELB"

  min_size = 2
  max_size = 3

  tag {
    key                 = "Name"
    value               = "benedetto-terraform-asg-example"
    propagate_at_launch = true
  }
}

resource "aws_elb" "elb" {
  name               = "benedetto-terraform-asg-example"
  availability_zones = ["${data.aws_availability_zones.all.names}"]
  security_groups = ["${aws_security_group.elbsg.id}"]

  listener {
    lb_port           = 80
    lb_protocol       = "http"
    instance_port     = "${var.server_port}"
    instance_protocol = "http"
  }
  health_check {
    healthy_threshold = 2
    unhealthy_threshold = 2
    timeout             = 3
    interval            = 30
    target              = "HTTP:${var.server_port}/"
  }
}

resource "aws_security_group" "elbsg" {
  name = "benedetto-terraform-example-elb"

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  egress {
    from_port = 0
    to_port = 0
    protocol = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}
